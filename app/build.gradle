plugins {
    id 'com.android.application' version '8.2.2'
    id 'org.jetbrains.kotlin.android' version '1.9.23'
    id 'org.jetbrains.kotlin.kapt' version '1.9.10'
}

android {
    namespace 'sibyllink.vnc'

    compileSdk 34
    ndkVersion "26.1.10909125"

    defaultConfig {
        applicationId "sibyllink.vnc"
        minSdk 30
        targetSdk 34
        versionCode 31
        versionName "2.4.2"
        resourceConfigurations += ['en', 'xhdpi']

        externalNativeBuild {
            cmake {
                //We are not using STL
                arguments '-DANDROID_STL=none'
                cFlags  '-Os', '-fvisibility=hidden'
                targets 'native-vnc', 'vncclient', 'turbojpeg-static', 'wolfssl'
            }
        }

        vectorDrawables {
            useSupportLibrary true
        }
    }
    

    buildTypes {

        debug {
            applicationIdSuffix '.debug'
            versionNameSuffix ' (debug)'
        }

        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles 'proguard-rules.pro'
            debuggable false
            jniDebuggable false
            renderscriptDebuggable false
            pseudoLocalesEnabled false
            crunchPngs true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }

        ci {
            initWith(release)
            applicationIdSuffix '.ci'
            versionNameSuffix ' (CI)'
        }
    }
    composeOptions {
        kotlinCompilerExtensionVersion = "1.5.11"
    }
    buildFeatures {
        buildConfig true
        compose true
    }

    externalNativeBuild {
        cmake {
            version '3.22.1'
            path file('CMakeLists.txt')
        }
    }
    splits{
        abi{ // Enables building multiple APKs per ABI.
            enable true

            // Resets the list of ABIs for Gradle to create APKs for to none.
            reset()

            // Specifies a list of ABIs for Gradle to create APKs for.
            include "x86_64",'armeabi-v7a'
            universalApk false
        }
    }

    bundle {
        density {
            enableSplit true
        }
        language {
            enableSplit true
        }

    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    packaging {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
}

kotlin {
    jvmToolchain(17)
}

dependencies {
    implementation 'androidx.compose.ui:ui'

    // Android Studio Preview support
    implementation 'androidx.compose.ui:ui-tooling-preview'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.7.0'
    implementation 'androidx.activity:activity-compose:1.8.2'
    implementation 'androidx.compose.ui:ui-graphics'

    implementation platform('androidx.compose:compose-bom:2024.03.00')
    implementation "androidx.wear.compose:compose-material:1.4.0-alpha05"
    implementation "androidx.wear.compose:compose-foundation:1.4.0-alpha05"
    implementation("androidx.compose.material:material-icons-extended:1.6.4")
    implementation ("com.google.android.horologist:horologist-compose-layout:0.6.3")

    implementation "androidx.core:core-ktx:1.12.0"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.7.0"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:2.7.0"
}